
//tx_rx_rgb  Robert Hart Aug 2019.

//  Program to use transmit-receive across space between two conductors.
//
//
//  This program has a function "tx_rx() which returns the value in a long integer.
//
//  Optionally, two resistors (1 MOhm or greater) can be placed between 5V and GND, with
//  the signal connected between them so that the steady-state voltage is 2.5 Volts.
//
//  Signal varies with electric field coupling between conductors, and can
//  be used to measure many things related to position, overlap, and intervening material
//  between the two conductors.
//
//  This version of the program uses a RGB LED as an output device.

//long result;  //for debugging
byte analog_pin = A3;
byte tx_pin = A1;
byte red_pin = 3;
byte green_pin = 5;
byte blue_pin = 6;
byte pins[3] = {red_pin, green_pin, blue_pin};
int i = -1;

void setup() {
pinMode(tx_pin,OUTPUT);      //Pin that provides the voltage step
Serial.begin(9600);

for(byte j=0; j<3; j++){      //Turn them all off
  analogWrite(pins[j], 255);
  }

}


long tx_rx(){         //Function to execute rx_tx algorithm and return a value
                      //that depends on coupling of two electrodes.
                      //Value returned is a long integer.
  int read_high;
  int read_low;
  int diff;
  long int sum;
  int N_samples = 100;    //Number of samples to take.  Larger number slows it down, but reduces scatter.

  sum = 0;

  for (int i = 0; i < N_samples; i++){
   digitalWrite(tx_pin,HIGH);              //Step the voltage high on conductor 1.
   read_high = analogRead(analog_pin);        //Measure response of conductor 2.
   delayMicroseconds(100);            //Delay to reach steady state.
   digitalWrite(tx_pin,LOW);               //Step the voltage to zero on conductor 1.
   read_low = analogRead(analog_pin);         //Measure response of conductor 2.
   diff = read_high - read_low;       //desired answer is the difference between high and low.
 sum += diff;                       //Sums up N_samples of these measurements.
 }
  return sum;
}                         //End of tx_rx function.


void loop() {       //This is the program part that calls the function and outputs to serial.
//result = tx_rx();
//Serial.println(result);

while(tx_rx()<11000){      //wait until tx-rx signal is above threshold.
}

i++;
if(i>2) i=0;

for(byte j=0; j<3; j++){
  analogWrite(pins[j], 255);
}

analogWrite(pins[i],0);
delay(200);
}
